:start ::= Document
:discard ~ whitespace
:discard ~ Comment
Document ::= Shebang? LanguageDeclaration? Body
Shebang ~ '#!' AbsoluteFilePath ShebangParam* eol+
ShebangParam ~ ' ' any
LanguageDeclaration ~ '#language ' URI ';'
Body ::= Pragma* DefaultRelationDeclaration? Pragma* ElementList ';'?
Subset ::= '{' Body '}' Transformed*
Transformed ::= modifiedby Transform
Pragma ~ PragmaBody ';'
PragmaBody ~ '#include ' FileURL
           | '#import ' FileURL
           | '#layout ' FileURI
           | '#glyphs ' FileURL
DefaultRelationDeclaration ~ '#using ' RelationID ';'
FileURL ~ quo FilePath quo
        | URL
FileURI ~ quo FilePath quo
        | URI
FilePath ~ RelativeFilePath
         | AbsoluteFilePath
AbsoluteFilePath ~ FSRoot RelativeFilePath
RelativeFilePath ~ PathPart PathContinuation* PathSep Filename
PathContinuation ~ PathSep PathPart
Element ::= Declaration
          | Graph
          | Subset
          | ElementList
ElementList ::= Element ';' ElementList
              | Element
Comment ::= '/*' any '*/'
Declaration ::= Variable equals Subset
              | Variable equals Clause
              | Macro copies Subset
              | Macro copies Clause
              | TransformID lookslike TransDefList
              | AppearanceID lookslike AppearanceDef
              | InterfaceID lookslike InterfaceDef
              | RelationID has Appearance DerivedAppearance*
              | GlyphID has Profile DerivedProfile*
              | RelationID is RelationID DerivedAppearance+
              | GlyphID is GlyphID DerivedProfile+
Profile ::= Appearance
          | Interface
DerivedProfile ~ with_without Appearance
               | with_without Interface
               | Transformed
with_without ~ with
             | without
DerivedAppearance ::= with_without Appearance
InterfaceDef ::= '(' Port ( ',' Port )* ')'
AppearanceDef ::= '[' Feature ( ',' Feature )* ']'
Transform ::= TransformID
            | TransDefList
Interface ::= InterfaceID
            | InterfaceDef
Appearance ::= AppearanceID
             | AppearanceDef
Graph ::= LHS Edge ( MHS Edge )* RHS Transformed*
LHS ::= LHSJoin
      | Joinable ( '(' LHSJoin ')' )?
LHSJoin ::= Glyph Join?
MHS ::= Join? Glyph Join?
      | ( '(' RHSJoin ')' )? Joinable ( '(' LHSJoin ')' )?
RHS ::= RHSJoin
      | ( '(' RHSJoin ')' )? Joinable
RHSJoin ::= Join? Glyph
Join ::= '(' Port ')'
Joinable ::= Clause
            | Reference
Glyph ::= GlyphID
        | Reference
Reference ::= Macro
            | Variable
Edge ::= RelSource Relationship RelTarget
       | DefaultRelation
DefaultRelation ::= '->'
                  | '-->'
                  | '<->'
                  | '<-->'
RelSource ::= '-['
            | '--['
            | '<-['
RelTarget ::= ']->'
Relationship ::= RelationID ( ',' RelationID )*
               | Relationship '+' Appearance
TransDefList ::= '[' TransformDef ( ',' TransformDef )* ']'
TransformDef ::= Condition? TypedTransform
Condition ::= FeatureExpression '?'
TypedTransform ::= Relationship becomes Relationship
                 | Clause becomes Clause
                 | Graph becomes Graph
                 | ( Appearance becomes )? Appearance
FeatureExpression ::= DefiniteFeature ( 'or'? FeatureExpression )*
                    | '(' FeatureExpression ')'
Feature ::= DefiniteFeature
          | undef FeatureID
DefiniteFeature ::= set FeatureID
                  | clear FeatureID
Clause ::= '(' Graph MoreGraphs* ')'
MoreGraphs ::= ',' Graph
Variable ~ '$'? Name
         | '$' Number
Macro ::= Name
Transform ::= Name
GlyphID ~ '#'? Name
        | '#' Number
RelationID ~ '&'? Name
           | '&' Number
FeatureID ~ '%'? Name
          | '%' Number
InterfaceID ~ '@:'? Name
            | '@:' Number
AppearanceID ~ '%:'? Name
             | '%:' Number
Port ~ '@'? Name
     | '@' Number
Number ~ [\p{Digit}]+
Name ~ NMSTART NMCHAR? PRIME*
NMSTART ~ [\p{Letter}_]
NMCHAR ~  [\p{Letter}\p{Digit}_.\@\$\%/:+-]* [\p{Letter}\p{Digit}_]
PRIME ~ [\x{27}\x{2B9}\x{2BC}\x{2C8}\x{2CA}\x{2019}\x{2032}]
whitespace ~ [\p{Separator}]+
quo ~ [']
any ~ [[:graph:]]+?
eol ~ '\n'
equals ~ '='
copies ~ ':'
lookslike ~ '~'
has ~ 'has'
is ~ 'is'
with ~ '+'
without ~ '-'
set ~ '+'
clear ~ '-'
undef ~ '~'
becomes ~ '>'
modifiedby ~ '*'
