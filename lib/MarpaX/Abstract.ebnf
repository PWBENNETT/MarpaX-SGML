:default ::= action => [values] bless => ::lhs
:start ::= PotentiallySystemizedDocument
event ATTR = completed AttributeSpecification
event ATTRVAL = completed AttributeValueSpecification
event COMMENT = completed CommentDeclaration
event CONTENT = completed Content
event CRUFT = completed NonSGMLData
event DECL = completed AttributeDefinitionListDeclaration
event DECL = completed ElementDeclaration
event DECL = completed EntityDeclaration
event DECL = completed IDLinkSetDeclaration
event DECL = completed LinkSetDeclaration
event DECL = completed LinkSetUseDeclaration
event DECL = completed MarkedSectionDeclaration
event DECL = completed NotationDeclaration
event DECL = completed SGMLDeclaration
event DECL = completed ShortReferenceMappingDeclaration
event DECL = completed ShortReferenceUseDeclaration
event DTD = completed DTD
event ELEMENT = completed Element
event ENTITY = completed CharacterDataEntity
event ENTITY = completed SGMLDocumentEntity
event ENTITY = completed SGMLSubDocumentEntity
event ENTITY = completed SGMLTextEntity
event ENTITY = completed SpecificCharacterDataEntity
event LTD = completed LTD
event PI = completed ProcessingInstruction
event PROLOG = completed Prolog
event REF = completed CharacterReference
event REF = completed GeneralEntityReference
event REF = completed ParameterEntityReference
event SGML = completed SGMLDocument
event SYSTEM = completed SystemDeclaration
event TAG = completed EndTag
event TAG = completed StartTag
event TOP = completed PotentiallySystemizedDocument
event UNCLOSED = completed UnclosedEndTag
event UNCLOSED = completed UnclosedStartTag
event UNMATCHED = completed UnmatchedEnd
event UNMATCHED = completed UnmatchedStart
lexeme default = action => [start,length,value] bless => ::name
s ~ <s single>
<s single> ~ RE
           | RS
           | SPACE
           | SEPCHAR
<s opt> ~ <s single>
        | Nothing
<s star> ~ <s single>*
<s plus> ~ <s single>+
ts ::= s
     | Nil
     | ParameterEntityReference
ps ::= ts
     | Nil
     | CommentDeclaration
ds ::= ps
     | Nil
     | ProcessingInstruction
     | MarkedSectionDeclaration
Nil ~ Nothing
PotentiallySystemizedDocument ::= OpeningSystemDeclarations SGMLDocument
OpeningSystemDeclarations ::= <s star> SystemDeclarationList
                            | Nil
SystemDeclarationList ::= SystemDeclaration <s star> SystemDeclarationList
                        | SystemDeclaration
SGMLDocument ::= SGMLDocumentEntity SGMLDocumentAtoms <s star>
               | SGMLDocumentEntity <s star>
SGMLDocumentAtoms ::= SGMLDocumentAtom SGMLDocumentAtoms
                    | SGMLDocumentAtom
SGMLDocumentAtom ::= SGMLSubDocumentEntity
                   | SGMLTextEntity
                   | CharacterDataEntity
                   | SpecificCharacterDataEntity
                   | NonSGMLData
SGMLDocumentEntity ::= <s star> SGMLDeclaration Prolog DocumentInstanceSet
SGMLSubDocumentEntity ::= Prolog DocumentInstanceSet
SGMLTextEntity ::= SGMLCharacter
                 | Nil
CharacterDataEntity ::= SGMLCharacter
                      | Nil
SpecificCharacterDataEntity ::= SGMLCharacter
                              | Nil
NonSGMLData ::= Characters
Prolog ::= OPList DTD DTDOP LTDOP
DTDOP ::= DTD DTDOP
        | OtherProlog DTDOP
        | Nil
LTDOP ::= LTD LTDOP
        | OtherProlog LTDOP
        | Nil
OPList ::= OtherProlog s OPList
         | OtherProlog
OtherProlog ::= CommentDeclaration
              | ProcessingInstruction
              | s
DocumentInstanceSet ::= BaseDocumentElement OPList
                      | BaseDocumentElement
BaseDocumentElement ::= DocumentElement
DocumentElement ::= Element
Element ::= Matched
          | UnmatchedEnd
          | UnmatchedStart
Matched ::= StartTag Content EndTag
UnmatchedEnd ::= Content EndTag
UnmatchedStart ::= StartTag Content
StartTag ::= stago TagType AttributeSpecifications <s opt> tagc
           | MinimizedStartTag
MinimizedStartTag ::= EmptyStartTag
                    | UnclosedStartTag
                    | NetEnablingStartTag
EmptyStartTag ::= stago tagc
UnclosedStartTag ::= stago TagType AttributeSpecifications <s opt>
NetEnablingStartTag ::= stago NetTagType AttributeSpecifications <s opt> neto
EndTag ::= etago TagType <s opt> tagc
         | MinimizedEndTag
MinimizedEndTag ::= EmptyEndTag
                  | UnclosedEndTag
                  | NullEndTag
EmptyEndTag ::= etago tagc
UnclosedEndTag ::= etago TagType <s opt>
NullEndTag ::= netc
TagType ::= DocumentTypeSpecification GenericIdentifierSpecification
NetTagType ::= GenericIdentifierSpecification
Content ::= MixedContent
          | ElementContent
          | ReplaceableCharacterData
          | CharacterData
          | Nil
MixedContent ::= DataCharacter
               | ElementContent
ElementContent ::= Element
                 | OtherContent
OtherContent ::= CommentDeclaration
               | ShortReferenceUseDeclaration
               | LinkSetUseDeclaration
               | ProcessingInstruction
               | shortref
               | CharacterReference
               | GeneralEntityReference
               | MarkedSectionDeclaration
DocumentTypeSpecification ::= NameGroup
                            | Nil
GenericIdentifierSpecification ::= GenericIdentifier
                                 | RankStem
GenericIdentifier ::= Name
AttributeSpecifications ::= <s star> AttributeSpecificationPlus
                          | Nil
AttributeSpecificationPlus ::= AttributeSpecification <s star> AttributeSpecificationPlus
                             | AttributeSpecification
AttributeSpecification ::= Name <s star> vi <s star> AttributeValueSpecification
                         | AttributeValueSpecification
AttributeValueSpecification ::= AttributeValue
                              | AttributeValueLiteral
AttributeValueLiteral ::= lit  ReplaceableCharacterData lit
                        | lita ReplaceableCharacterData lita
                        | lit lit
                        | lita lita
AttributeValue ::= CharacterData
                 | GeneralEntityName
                 | GeneralEntityNameList
                 | IDValue
                 | IDReferenceValue
                 | IDReferenceList
                 | Name
                 | NameList
                 | NameToken
                 | NameTokenList
                 | NotationName
                 | Number
                 | NumberList
                 | NumberToken
                 | NumberTokenList
GeneralEntityNameList ::= NameList
IDValue ::= Name
IDReferenceList ::= NameList
IDReferenceValue ::= Name
NotationName ::= Name
Space ~ SPACE
NameList ::= Name Space NameList
           | Name
NumberList ::= Number Space NumberList
             | Number
NumberTokenList ::= NumberToken Space NumberTokenList
                  | NumberToken
NameTokenList ::= NameToken Space NameTokenList
                | NameToken
ProcessingInstruction ::= pio SystemDataPlus pic
                        | pio pic
SystemDataPlus ::= SystemData SystemDataPlus
                 | SystemData
SystemData ::= CharacterData
ReplaceableCharacterData ::= ReplaceableCharacterDataAtom ReplaceableCharacterData
                           | ReplaceableCharacterDataAtom
ReplaceableCharacterDataAtom ::= DataCharacter
                               | CharacterReference
                               | GeneralEntityReference
CharacterData ::= DataCharacterPlus
DataCharacterPlus ::= DataCharacter DataCharacterPlus
                    | DataCharacter
DataCharacter ::= SGMLCharacterAtom
Characters ::= Character Characters
             | Character
Character ::= SGMLCharacter
            | NONSGML
SGMLCharacter ::= SGMLCharacterAtom SGMLCharacter
                | SGMLCharacterAtom
SGMLCharacterAtom ::= MarkupCharacter
                    | DATACHAR
MarkupCharacter ::= NameCharacter
                  | FunctionCharacter
                  | DELMCHAR
NameCharacter ~ NMCHAR
NMCHARS ~ NMCHAR*
NMCHAR ~ NMSTRT
       | DIGIT
       | LCNMCHAR
       | UCNMCHAR
NMSTRT ~ LCLetter
       | UCLetter
       | LCNMSTRT
       | UCNMSTRT
FunctionCharacter ~ RE
                  | RS
                  | SPACE
                  | SEPCHAR
                  | MSOCHAR
                  | MSICHAR
                  | MSSCHAR
                  | FUNCHAR
Name ~ NAME
NAME ~ NMSTRT NMCHARS
Number ~ DIGIT+
Digit ~ DIGIT
NUMBER ~ DIGIT+
NameToken ::= NameCharacter
NumberToken ::= Digit NameCharacter
GeneralEntityReference   ::= ero  DocumentTypeSpecification Name ReferenceEnd
ParameterEntityReference ::= pero DocumentTypeSpecification Name ReferenceEnd
ReferenceEnd ~ refc
               | refc RE
CharacterReference ::= cro FunctionName    ReferenceEnd
                     | cro CharacterNumber ReferenceEnd
FunctionName ::= 'RE'
               | 'RS'
               | 'SPACE'
               | Name
CharacterNumber ::= Number
ParameterLiteral ::= lit  ReplaceableParameterData lit
                   | lita ReplaceableParameterData lita
                   | lit lit
                   | lita lita
ReplaceableParameterData ::= ReplaceableParameterDataAtom ReplaceableParameterData
                           | ReplaceableParameterDataAtom
ReplaceableParameterDataAtom ::= DataCharacter
                               | CharacterReference
                               | ParameterEntityReference
NameTokenGroup ::= grpo ts NameTokens ts grpc
NameTokens ::= NameToken Connection NameTokens
             | NameToken
NameGroup ::= grpo ts Names ts grpc
Names ::= Name Connection Names
        | Name
Connection ::= ts Connector
AssociatedElementType ::= GenericIdentifier
                        | NameGroup
ExternalIdentifier ::= 'SYSTEM'
                     | 'PUBLIC' ps PublicIdentifier
                     | 'SYSTEM' ps SystemIdentifier
                     | 'PUBLIC' ps PublicIdentifier ps SystemIdentifier
PublicIdentifier ::= MinimumLiteral
SystemIdentifier ::= lit  SystemDataPlus lit
                   | lita SystemDataPlus lita
                   | lit lit
                   | lita lita
MinimumLiteral ::= lit  MinimumData lit
                 | lita MinimumData lita
                 | lit lit
                 | lita lita
MinimumData ::= MinimumDataCharacter MinimumData
              | MinimumDataCharacter
MinimumDataCharacter ~ RS
                       | RE
                       | SPACE
                       | LCLetter
                       | UCLetter
                       | DIGIT
                       | Special
FormalPublicIdentifier ::= OwnerIdentifier slashslash TextIdentifier
OwnerIdentifier ::= ISOOwnerIdentifier
                  | RegisteredOwnerIdentifier
                  | UnregisteredOwnerIdentifier
ISOOwnerIdentifier ::= MinimumData
RegisteredOwnerIdentifier ::= plus slashslash MinimumData
UnregisteredOwnerIdentifier ::= minus slashslash MinimumData
TextIdentifier ::= PublicTextClass Space UnavailableTextIndicator PublicTextGuts slashslash PublicTextLanguage PTDV
PublicTextGuts ::= PublicTextDescription
                 | PublicTextDesignatingSequence
PTDV ::= slashslash PublicTextDisplayVersion
       | Nil
PublicTextDisplayVersion ::= DATACHAR PublicTextDisplayVersion
                           | DATACHAR
UnavailableTextIndicator ::= minus slashslash
                           | Nil
PublicTextClass ::= Name
PublicTextDescription ::= ISOTextDescription
                        | MinimumData
ISOTextDescription ::= MinimumData
PublicTextLanguage ::= Name
PublicTextDesignatingSequence ::= Name
CommentDeclaration ::= mdo CommentList mdc
CommentList ::= Comment s CommentList
              | Comment
Comment ::= com SGMLCharacter com
MarkedSectionDeclaration ::= MarkedSectionStart StatusKeywordSpecification dso MarkedSection MarkedSectionEnd
MarkedSectionStart ::= mdo dso
MarkedSectionEnd ::= msc mdc
MarkedSection ::= SGMLCharacter
StatusKeywordSpecification ::= ps StatusKeyword ps
                             | ps StatusKeyword
StatusKeyword ::= 'TEMP'
                | 'CDATA'
                | 'IGNORE'
                | 'INCLUDE'
                | 'RCDATA'
EntityDeclaration ::= mdo 'ENTITY' ps EntityName ps EntityText ps mdc
EntityName ::= GeneralEntityName
             | ParameterEntityName
GeneralEntityName ::= Name
                    | rni 'DEFAULT'
ParameterEntityName ::= pero ps Name
EntityText ::= ParameterLiteral
             | DataText
             | BracketedText
             | ExternalEntitySpecification
DataText ::= DataTextGuts ps ParameterLiteral
DataTextGuts ::= 'CDATA'
               | 'SDATA'
               | 'PI'
BracketedText ::= BracketedTextGuts ps ParameterLiteral
BracketedTextGuts ::= 'STARTTAG'
                    | 'ENDTAG'
                    | 'MS'
                    | 'MD'
ExternalEntitySpecification ::= ExternalIdentifier
                              | ExternalIdentifier ps EntityType
EntityType ::= 'SUBDOC'
             | 'NDATA' ps NotationName DataAttributeSpecification
             | 'CDATA' ps NotationName DataAttributeSpecification
             | 'SDATA' ps NotationName DataAttributeSpecification
DTD ::= mdo 'DOCTYPE' ps DocumentTypeName OptionalEI OptionalWrappedDTDSubset ps mdc
OptionalEI ::= ps ExternalIdentifier
             | Nil
OptionalWrappedDTDSubset ::= WrappedDTDSubset
                           | Nil
WrappedDTDSubset ::= ps WrappedDTDSubsets
WrappedDTDSubsets ::= dso DTDSubsets dsc
DocumentTypeName ::= GenericIdentifier
DTDSubsets ::= DTDSubset DTDSubsets
             | DTDSubset
DTDSubset ::= EntitySets
            | ElementSets
            | ShortReferenceSets
EntitySets ::= EntitySet EntitySets
             | EntitySet
EntitySet ::= EntityDeclaration
            | ds
ElementSets ::= ElementSet ElementSets
              | ElementSet
ElementSet ::= ElementDeclaration
             | AttributeDefinitionListDeclaration
             | NotationDeclaration
             | ds
ShortReferenceSets ::= ShortReferenceSet ShortReferenceSets
                     | ShortReferenceSet
ShortReferenceSet ::= EntityDeclaration
                    | ShortReferenceMappingDeclaration
                    | ShortReferenceUseDeclaration
                    | ds
ElementDeclaration ::= mdo 'ELEMENT' ps ElementType OptionalOTM ps DeclaredContent mdc
                     | mdo 'ELEMENT' ps ElementType OptionalOTM ps ContentModel    mdc
OptionalOTM ::= OTM
              | Nil
OTM ::= ps OmittedTagMinimization
ElementType ::= GenericIdentifier
              | NameGroup
              | RankedElement
              | RankedGroup
RankedElement ::= RankStem ps RankSuffix
RankedGroup ::= grpo ts RankStems ts grpc ps RankSuffix
RankStems ::= RankStem Connection RankStems
            | RankStem
RankStem ::= Name
RankSuffix ::= Number
OmittedTagMinimization ::= StartTagMinimization ps EndTagMinimization
StartTagMinimization ::= 'O'
                       | '-'
EndTagMinimization ::= 'O'
                     | '-'
DeclaredContent ::= 'CDATA'
                  | 'RCDATA'
                  | 'EMPTY'
ContentModel ::= ModelGroup OptionalExceptions
               | 'ANY' OptionalExceptions
OptionalExceptions ::= ps Exceptions
                     | Nil
ModelGroup ::= grpo ts ContentTokens ts grpc
ContentTokens ::= ContentToken Connection ContentTokens
                | ContentToken
ContentToken ::= PrimitiveContentToken
               | ModelGroup
PrimitiveContentToken ::= rni 'PCDATA'
                        | ElementToken
                        | DataTagGroup
ElementToken ::= GenericIdentifier
               | GenericIdentifier OccurenceIndicator
Connector ::= and
            | or
            | seq
OccurenceIndicator ::= opt
                     | plus
                     | rep
DataTagGroup ::= dtgo ts GenericIdentifier ts seq ts DataTagPattern ts dtgc
               | dtgo ts GenericIdentifier ts seq ts DataTagPattern ts dtgc OccurenceIndicator
DataTagPattern ::= DataTagTemplate      OptionalDataTagPaddingTemplate
                 | DataTagTemplateGroup OptionalDataTagPaddingTemplate
OptionalDataTagPaddingTemplate ::= ts seq DataTagPaddingTemplate
                                 | Nil
DataTagTemplateGroup ::= grpo ts DataTagTemplateList ts grpc
DataTagTemplateList ::= DataTagTemplate ts or ts DataTagTemplateList
                      | DataTagTemplate
DataTagTemplate ::= ParameterLiteral
DataTagPaddingTemplate ::= ParameterLiteral
Exceptions ::= Exclusions
             | Exclusions ps Inclusions
             | Inclusions
Inclusions ::= plus NameGroup
Exclusions ::= minus NameGroup
AttributeDefinitionListDeclaration ::= mdo 'ATTLIST' ps AssociatedElementType  AttributeDefinitions ps mdc
                                     | mdo 'ATTLIST' ps AssociatedNotationName AttributeDefinitions ps mdc
AttributeDefinitions ::= AttributeDefinition AttributeDefinitions
                       | AttributeDefinition
AttributeDefinition ::= ps AttributeName ps DeclaredValue ps DefaultValue
AttributeName ::= Name
DeclaredValue ::= 'CDATA'
                | 'ENTITY'
                | 'ENTITIES'
                | 'ID'
                | 'IDREF'
                | 'IDREFS'
                | 'NAME'
                | 'NAMES'
                | 'NMTOKEN'
                | 'NMTOKENS'
                | 'NUMBER'
                | 'NUMBERS'
                | 'NUTOKEN'
                | 'NUTOKENS'
                | 'NOTATION' ps NameGroup
                | NameTokenGroup
DefaultValue ::= DVFixedMarker AttributeValueSpecification
               | AttributeValueSpecification
               | rni DVMagicMarker
DVFixedMarker ::= rni 'FIXED' ps
DVMagicMarker ::= 'REQUIRED'
                | 'CURRENT'
                | 'CONREF'
                | 'IMPLIED'
NotationDeclaration ::= mdo 'NOTATION' ps NotationName
                                       ps NotationIdentifier
                                       ps
                        mdc
                      | mdo 'NOTATION' ps NotationName
                                       ps NotationIdentifier
                        mdc
NotationIdentifier ::= ExternalIdentifier
AssociatedNotationName ::= rni 'NOTATION' ps NotationName
                         | rni 'NOTATION' ps NameGroup
DataAttributeSpecification ::= ps dso AttributeSpecificationPlus <s star> dsc
ShortReferenceMappingDeclaration ::= mdo 'SHORTREF' ps MapName PLNmPlus ps mdc
PLNmPlus ::= PLNm PLNmPlus
           | PLNm
PLNm ::= ps ParameterLiteral ps Name
MapName ::= Name
ShortReferenceUseDeclaration ::= mdo 'USEMAP' ps MapSpecification AETGuts ps mdc
AETGuts ::= ps AssociatedElementType
          | Nil
MapSpecification ::= MapName
                   | rni 'EMPTY'
LTD ::= mdo 'LINKTYPE' ps LinkTypeName ps LinkSpecification ExtID WrappedLTDSubset ps mdc
LinkSpecification ::= SimpleLinkSpecification
                    | ImplicitLinkSpecification
                    | ExplicitLinkSpecification
ExtID ::= ps ExternalIdentifier
        | Nil
WrappedLTDSubset ::= ps dso LTDSubset dsc
                   | Nil
LinkTypeName ::= Name
SimpleLinkSpecification ::= rni 'SIMPLE' ps rni 'IMPLIED'
ImplicitLinkSpecification ::= SourceDocumentTypeName ps rni 'IMPLIED'
ExplicitLinkSpecification ::= SourceDocumentTypeName ps ResultDocumentTypeName
SourceDocumentTypeName ::= DocumentTypeName
ResultDocumentTypeName ::= DocumentTypeName
LTDSubset ::= OptionalLinkSets OptionalIDLinkSetDeclaration OptionalLinkSets
OptionalIDLinkSetDeclaration ::=IDLinkSetDeclaration
                               | Nil
OptionalLinkSets ::= LinkSets
                   | Nil
LinkSets ::= LinkSet LinkSets
           | LinkSet
LinkSet ::= LinkAttributeSet
          | LinkSetDeclaration
LinkAttributeSet ::= AttributeDefinitionListDeclaration
                   | EntitySet
LinkSetDeclaration ::= mdo 'LINK' ps LinkSetName
                                  ps LinkRule
                                  ps
                       mdc
                     | mdo 'LINK' ps LinkSetName
                                  ps LinkRule
                       mdc
LinkSetUseDeclaration ::= DATACHAR LinkSetUseDeclaration
                        | DATACHAR
LinkRule ::= SourceElementSpecification
           | ExplicitLinkRule
LinkSetName ::= Name
              | rni 'INITIAL'
SourceElementSpecification ::= AssociatedElementType Uselink Postlink LinkAttributeSpecification
Uselink ::= ps rni 'USELINK' ps LSN
          | Nil
LSN ::= LinkSetName
      | rni 'EMPTY'
Postlink ::= ps rni 'POSTLINK' ps LinkSetSpecification
           | Nil
LinkAttributeSpecification ::= s dso AttributeSpecificationPlus s dsc
                             | Nil
ExplicitLinkRule ::= SourceElementSpecification ps ResultElementSpecification
                   | SourceElementSpecification ps rni 'IMPLIED'
                   | rni 'IMPLIED' ps ResultElementSpecification
ResultElementSpecification ::= GenericIdentifier
                             | GenericIdentifier ResultAttributeSpecification
ResultAttributeSpecification ::= s dso AttributeSpecificationPlus dsc
IDLinkSetDeclaration ::= mdo 'USELINK' ps LinkSetSpecification
                                       ps LinkTypeName
                                       ps
                         mdc
                       | mdo 'USELINK' ps LinkSetSpecification
                                       ps LinkTypeName
                         mdc
LinkSetSpecification ::= LinkSetName
                       | rni 'EMPTY'
                       | rni 'RESTORE'
SGMLDeclaration ::= mdo 'SGML' ps MinimumLiteral
                               ps DocumentCharacterSet
                               ps CapacitySet
                               ps ConcreteSyntaxScope
                               ps ConcreteSyntax
                               ps FeatureUse
                               ps ApplicationSpecificInformation
                               ps
                    mdc
                  | mdo 'SGML' ps MinimumLiteral
                               ps DocumentCharacterSet
                               ps CapacitySet
                               ps ConcreteSyntaxScope
                               ps ConcreteSyntax
                               ps FeatureUse
                               ps ApplicationSpecificInformation
                    mdc
DocumentCharacterSet ::= 'CHARSET' CharacterSetDescriptions
CharacterSetDescriptions ::= CharacterSetDescription CharacterSetDescriptions
                           | CharacterSetDescription
CharacterSetDescription ::= ps BaseCharacterSet ps DescribedCharacterSetPortion
BaseCharacterSet ::= 'BASESET' ps PublicIdentifier
DescribedCharacterSetPortion ::= 'DESCSET' ps CharacterDescriptions
                               | Nil
CharacterDescriptions ::= CharacterDescription ps CharacterDescriptions
                        | CharacterDescription
CharacterDescription ::= DescribedSetCharacterNumber ps NumberOfCharacters ps CharDescChoice
CharDescChoice ::= BaseSetCharacterNumber
                 | MinimumLiteral
                 | 'UNUSED'
DescribedSetCharacterNumber ::= CharacterNumber
BaseSetCharacterNumber ::= CharacterNumber
NumberOfCharacters ::= Number
CapacitySet ::= 'CAPACITY' ps CapacityGuts
CapacityGuts ::= PublicCapacity
               | SGMLRefCapacity
PublicCapacity ::= 'PUBLIC' ps PublicIdentifier
SGMLRefCapacity ::= 'SGMLREF' SGMLRefCapacityListGuts
SGMLRefCapacityListGuts ::= SGMLRefCapacityGuts SGMLRefCapacityListGuts
                          | SGMLRefCapacityGuts
SGMLRefCapacityGuts ::= ps Name ps Number
ConcreteSyntaxScope ::= 'SCOPE' ps 'DOCUMENT'
                      | 'SCOPE' ps 'INSTANCE'
ConcreteSyntax ::= 'SYNTAX' ps PublicConcreteSyntax
                 | 'SYNTAX' ps PrivateConcreteSyntax
PrivateConcreteSyntax ::= ShunnedCharacterNumberIdentification
                          ps SyntaxReferenceCharacterSet
                          ps FunctionCharacterIdentification
                          ps NamingRules
                          ps DelimiterSet
                          ps ReservedNameUse
                          ps QuantitySet
PublicConcreteSyntax ::= 'PUBLIC' ps PublicIdentifier
                       | 'PUBLIC' ps PublicIdentifier PublicSwitches
PublicSwitches ::= 'SWITCHES' PublicSwitchPlus
PublicSwitchPlus ::= PublicSwitch PublicSwitchPlus
                   | PublicSwitch
PublicSwitch ::= ps CharacterNumber ps CharacterNumber
ShunnedCharacterNumberIdentification ::= 'SHUNCHAR' ps 'NONE'
                                       | 'SHUNCHAR' ps 'CONTROLS' ps SCNList
SCNList ::= CharacterNumber ps SCNList
          | CharacterNumber
SyntaxReferenceCharacterSet ::= CharacterSetDescription
FunctionCharacterIdentification ::= 'FUNCTION' ps 'RE' ps CharacterNumber
                                               ps 'RS' ps CharacterNumber
                                               ps 'SPACE' ps CharacterNumber
                                               AddedFunctionCharacters
AddedFunctionCharacters ::= ps AddedFunctionCharacterList
                          | Nil
AddedFunctionCharacterList ::= AddedFunctionCharacter ps AddedFunctionCharacterList
                             | AddedFunctionCharacter
AddedFunctionCharacter ::= AddedFunction ps FunctionClass ps CharacterNumber
AddedFunction ::= Name
FunctionClass ::= 'FUNCHAR'
                | 'MSICHAR'
                | 'MSOCHAR'
                | 'MSSCHAR'
                | 'SEPCHAR'
NamingRules ::= 'NAMING' ps 'LCNMSTART' ps ParameterLiteral
                         ps 'UCNMSTART' ps ParameterLiteral
                         ps 'LCNMCHAR'  ps ParameterLiteral
                         ps 'UCNMCHAR'  ps ParameterLiteral
                         ps 'NAMECASE'  ps 'GENERAL' ps Boolean
                         ps 'ENTITY'    ps Boolean
Boolean ::= 'YES'
          | 'NO'
CountedBoolean ::= 'YES' ps Number
                 | 'NO'
ASN1Boolean ::= 'YES' ps 'ASN1'
              | 'YES'
              | 'NO'
DelimiterSet ::= 'DELIM' ps GeneralDelimiters ps ShortReferenceDelimiters
GeneralDelimiters ::= 'GENERAL' ps 'SGMLREF' ps SGMLDelims
SGMLDelims ::= SGMLDelim ps SGMLDelims
             | SGMLDelim
SGMLDelim ::= Name ps ParameterLiteral
ShortReferenceDelimiters ::= 'SHORTREF' ps 'SGMLREF' ParameterLiterals
                           | 'SHORTREF' ps 'NONE' ParameterLiterals
ParameterLiterals ::= ps ParameterLiteralList
                    | Nil
ParameterLiteralList ::= ParameterLiteral ps ParameterLiteralList
                       | ParameterLiteral
ReservedNameUse ::= 'NAMES' ps 'SGMLREF' NamePairs
NamePairs ::= ps NamePairList
            | Nil
NamePairList ::= NamePair ps NamePairList
               | NamePair
NamePair ::= Name ps Name
QuantitySet ::= 'QUANTITY' ps 'SGMLREF' ps NameQuantities
NameQuantities ::= NameQuantity ps NameQuantities
                 | NameQuantity
NameQuantity ::= Name ps Number
FeatureUse ::= 'FEATURES' ps MarkupMinimizationFeatures
                        ps LinkTypeFeatures
                        ps OtherFeatures
MarkupMinimizationFeatures ::= 'MINIMIZE' ps 'DATATAG' ps Boolean
                                          ps 'OMITTAG' ps Boolean
                                          ps 'RANK' ps Boolean
                                          ps 'SHORTTAG' ps Boolean
LinkTypeFeatures ::= 'LINK' ps 'SIMPLE' ps CountedBoolean
                            ps 'IMPLICIT' ps Boolean
                            ps 'EXPLICIT' ps CountedBoolean
OtherFeatures ::= 'OTHER' ps 'CONCUR' ps CountedBoolean
                          ps 'SUBDOC' ps CountedBoolean
                          ps 'FORMAL' ps Boolean
ApplicationSpecificInformation ::= 'APPINFO' ps 'NONE'
                                 | 'APPINFO' ps MinimumLiteral
SystemDeclaration ::= mdo 'SYSTEM' ps MinimumLiteral
                                   ps DocumentCharacterSet
                                   ps CapacitySet
                                   ps FeatureUse
                                   ps ConcreteSyntaxScope
                                   ps ConcreteSyntaxesSupportedPlus
                                   ps ValidationServices
                                   ps SDIFSupport
                                   ps
                      mdc
                    | mdo 'SYSTEM' ps MinimumLiteral
                                   ps DocumentCharacterSet
                                   ps CapacitySet
                                   ps FeatureUse
                                   ps ConcreteSyntaxScope
                                   ps ConcreteSyntaxesSupportedPlus
                                   ps ValidationServices
                                   ps SDIFSupport
                      mdc
ConcreteSyntaxesSupportedPlus ::= ConcreteSyntaxesSupported ConcreteSyntaxesSupportedPlus
                                | ConcreteSyntaxesSupported
ConcreteSyntaxesSupported ::= ps ConcreteSyntax
                            | ps ConcreteSyntax ps ConcreteSyntaxChanges
ConcreteSyntaxChanges ::= 'CHANGES' ps 'SWITCHES'
                        | 'CHANGES' ps 'DELIMLEN' ps Number
                                    ps 'SEQUENCE' ps Boolean
                                    ps 'SRCNT' ps Number
                                    ps 'SRLEN' ps Number
ValidationServices ::= 'VALIDATE' ps 'GENERAL' ps Boolean
                                  ps 'MODEL' ps Boolean
                                  ps 'EXCLUDE' ps Boolean
                                  ps 'CAPACITY' ps Boolean
                                  ps 'NONSGML' ps Boolean
                                  ps 'SGML' ps Boolean
                                  ps 'FORMAL' ps Boolean
SDIFSupport ::= 'SDIF' ps 'PACK' ps ASN1Boolean
                       ps 'UNPACK' ps ASN1Boolean
Nothing ~
RE ~ [\r]
RS ~ [\n]
SPACE ~ ' '
SEPCHAR ~ [\t]
LCLetter ~ [a-z]
UCLetter ~ [A-Z]
DIGIT ~ [0-9]
lit ~ ["]
lita ~ [']
Squo ~ [']
stago ~ '<'
etago ~ '</'
tagc ~ '>'
neto ~ '/'
netc ~ '/'
pio ~ '<?'
pic ~ '?>'
dtgo ~ '['
dtgc ~ ']'
mdo ~ '<!'
mdc ~ '>'
dso ~ '['
dsc ~ ']'
msc ~ ']]'
ero ~ '&'
pero ~ '%'
refc ~ ';'
cro ~ '&#'
grpo ~ '('
grpc ~ ')'
shortref ~ '&#TAB;'
         | '&#RE;'
         | '&#RS;'
         | '&#RS;B'
         | '&#RS;&#RE;'
         | '&#RS;B&#RE;'
         | 'B&#RE;'
         | '&#SPACE;'
         | 'BB'
         | '"'
         | '#'
         | '%'
         | Squo
         | '('
         | ')'
         | '*'
         | '+'
         | ','
         | '-'
         | '--'
         | ':'
         | ';'
         | '='
         | '@'
         | '['
         | ']'
         | '^'
         | '_'
         | '{'
         | '|'
         | '}'
         | '~'
vi ~ '='
plus ~ '+'
minus ~ '-'
slashslash ~ '//'
LCNMSTRT ~ [a-z]
UCNMSTRT ~ [A-Z]
LCNMCHAR ~ '-'
         | '.'
         | [a-z]
UCNMCHAR ~ [A-Z]
MSOCHAR ~ [\p{Separator}]
MSICHAR ~ [\p{Separator}]
MSSCHAR ~ [\p{Separator}]
FUNCHAR ~ [\p{Separator}]
Special ~ '('
        | ')'
        | '+'
        | ','
        | '-'
        | '.'
        | '/'
        | ':'
        | '='
        | '?'
        | Squo
com ~ '--'
rni ~ '#'
and ~ '&'
or ~ '|'
seq ~ ','
opt ~ '?'
rep ~ '*'
NONSGML ~ [\P{Assigned}]
DATACHAR ~ [\P{Separator}]
DELMCHAR ~ [\p{Separator}]
